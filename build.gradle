buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'com.github.koresframework'
version '2.0.0'

// Programming Language
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'java'

// License
apply plugin: 'license'

// Shade
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    //maven { url "https://jitpack.io" }
    // JwIUtils
    maven {
        url "https://gitlab.com/api/v4/projects/28895078/packages/maven"
    }
    // BytecodeDisassembler
    maven {
        url 'https://gitlab.com/api/v4/projects/28905629/packages/maven'
    }
    // Kores
    maven {
        url 'https://gitlab.com/api/v4/projects/28894889/packages/maven'
    }
    maven {
        url 'https://gitlab.com/api/v4/projects/28905266/packages/maven'
    }
    maven {
        url 'https://gitlab.com/api/v4/projects/28905428/packages/maven'
    }
    maven {
        url 'https://gitlab.com/api/v4/projects/29289915/packages/maven'
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"

    // Utilities
    implementation("com.koresframework:kores:4.2.8.base")
    implementation("com.koresframework:kores-bytecodewriter:4.2.8.bytecode")
    implementation("com.koresframework:kores-sourcewriter:4.2.4.source")
    implementation 'com.github.jonathanxd:bytecode-disassembler:2.4.1'
    implementation 'com.github.jonathanxd:kores-extra:1.5.1'

    implementation "com.github.jonathanxd:jwiutils:4.17.8"
    implementation "com.github.jonathanxd:jwiutils-kt:4.17.8"
    implementation "com.github.jonathanxd:specializations:4.17.8"
    implementation "io.github.lukehutch:fast-classpath-scanner:3.1.15"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

shadowJar {
    classifier = 'shaded'
}

tasks.dokkaHtml.configure {
    outputDirectory.set(javadoc.destinationDir)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokkaHtml) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

compileKotlin {
    kotlinOptions.jvmTarget = "16"
    kotlinOptions.freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "16"
    kotlinOptions.freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
}

test {
    useJUnit()
}

license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}

tasks.dokkaHtml.configure {
    enabled = false
}